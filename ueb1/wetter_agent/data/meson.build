pkgdatadir = join_paths(get_option('prefix'), get_option('datadir'), meson.project_name())
python = import('python')

conf = configuration_data()
conf.set('PYTHON', python.find_installation('python3').path())
conf.set('VERSION', meson.project_version())
conf.set('localedir', join_paths(get_option('prefix'), get_option('localedir')))
conf.set('pkgdatadir', pkgdatadir)

desktop_file = i18n.merge_file(
  input: 'de.falsei.ueb1wetter.desktop.in',
  output: 'de.falsei.ueb1wetter.desktop',
  type: 'desktop',
  po_dir: '../po',
  install: true,
  install_dir: join_paths(get_option('datadir'), 'applications')
)

desktop_utils = find_program('desktop-file-validate', required: false)
if desktop_utils.found()
  test('Validate desktop file', desktop_utils,
    args: [desktop_file]
  )
endif

appstream_file = i18n.merge_file(
  input: 'de.falsei.ueb1wetter.appdata.xml.in',
  output: 'de.falsei.ueb1wetter.appdata.xml',
  po_dir: '../po',
  install: true,
  install_dir: join_paths(get_option('datadir'), 'appdata')
)

appstream_util = find_program('appstream-util', required: false)
if appstream_util.found()
  test('Validate appstream file', appstream_util,
    args: ['validate', appstream_file]
  )
endif

install_data('de.falsei.ueb1wetter.gschema.xml',
  install_dir: join_paths(get_option('datadir'), 'glib-2.0/schemas')
)

compile_schemas = find_program('glib-compile-schemas', required: false)
if compile_schemas.found()
  test('Validate schema file', compile_schemas,
    args: ['--strict', '--dry-run', meson.current_source_dir()]
  )
endif

configure_file(
  input: 'wetter_agent.in',
  output: 'wetter_agent',
  configuration: conf,
  install: true,
  install_dir: get_option('bindir')
)


subdir('icons')
subdir('ui')
